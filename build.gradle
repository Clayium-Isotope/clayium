buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}
        
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

version = "0.0.0"
group = "clayium"
archivesBaseName = "clayium"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
	mappings channel: 'snapshot', version: '20171003-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                clayium {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                clayium {
                    source sourceSets.main
                }
            }
        }
    }

    accessTransformer = file('src/main/resources/META-INF/clayium_at.cfg')
}

sourceSets {
    main({ output.resourcesDir = output.classesDir })
}

dependencies {
    /* minecraft dependency is here */
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'

    // refer to: https://github.com/mezz/JustEnoughItems/issues/1959
    // compile against the JEI API but do not include it at runtime
    compileOnly fg.deobf("mezz.jei:jei_${mc_version}:${jei_version}:api")
    // at runtime, use the full JEI jar
    runtimeOnly fg.deobf("mezz.jei:jei_${mc_version}:${jei_version}")

    implementation ("net.minecraftforge:mergetool:0.2.3.3") { force = true }
}

tasks.withType(Jar) {
    compileJava.options.encoding = 'UTF-8'
}

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

// refer to: https://mcmodding.jp/modding/index.php/Access_Transformer利用
jar {
    manifest {
        attributes 'FMLAT': 'clayium_at.cfg'
    }
}

// https://stackoverflow.com/questions/68377027/minecraft-forge-mod-loader-fml-loading-and-crashing-mc
configurations.configureEach {
    exclude group: "net.minecraftforge", module: "mergetool"
}
